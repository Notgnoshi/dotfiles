execute pathogen#infect()

let g:gruvbox_italic=1
set bg=dark
" Wonky in SSH sessions
"set termguicolors
colorscheme gruvbox

" Custom GitGutter commands
command Stage  GitGutterStageHunk
command Revert GitGutterUndoHunk

" Set default encoding to UTF-8
set enc=utf-8

" Set autowrite for use with :make
set autowrite

" Number of visual spaces per TAB
set tabstop=4
" Number of spaces in tab when editing
set softtabstop=0
set expandtab
set shiftwidth=4
set smarttab
set backspace=indent,eol,start

"Use terminal title
set title
set titlestring=%F

" Show line numbers
set number
" Keep the cursor 6 lines from bottom of screen.
set scrolloff=6
" Toggle paste mode with f3
set pastetoggle=<F3>

set nocompatible
filetype plugin on
syntax on

" Vimwiki settings
" If :help vimwiki doesn't work, run :Helptags
set runtimepath+=~/Documents/notes/.vimwiki_rtp
let wiki = {}
let wiki.path = '~/Documents/notes'
let wiki.path_html = '~/Documents/notes/.vimwiki_html'
let wiki.syntax = 'markdown'
let wiki.ext = '.wiki'
let wiki.auto_tags = 1
let wiki.automatic_nested_syntaxes = 1
let wiki.nested_syntaxes = {'bash':'sh', 'shell':'sh', 'terminal':'sh', 'latex': 'tex'}
let g:vimwiki_list = [wiki]
let g:vimwiki_folding = 'expr'

" Allow fuzzy menu
set wildmenu
" Redraw screen lazily
set lazyredraw
"Matching brackets highlighted
set showmatch
" Highlight as characters are entered
set incsearch
" Must be turned off manually
set hlsearch
" Turn off search highlighting with enter
nnoremap <CR> :nohl<CR><CR>

" Use ctrl+[jk] to move lines up and down in normal, insert, and visual mode.
nnoremap <C-j> :m .+1<CR>==
nnoremap <C-k> :m .-2<CR>==
inoremap <C-j> <Esc>:m .+1<CR>==gi
inoremap <C-k> <Esc>:m .-2<CR>==gi
vnoremap <C-j> :m '>+1<CR>gv=gv
vnoremap <C-k> :m '<-2<CR>gv=gv

" enable code folding
set foldenable
set foldlevelstart=15
set foldnestmax=10
set foldmethod=syntax

nnoremap <space> za

" Visual movement
nnoremap j gj
nnoremap k gk

" Force vim to think .md are markdown files on older systems that don't
" have the filetype properly set in /usr/share/vim/vim74/filetype.vim
" (Looking at you $COMPANY dev servers)
autocmd BufNewFile,BufRead *.md set filetype=markdown
" Set Nginx conf filetype
au BufRead,BufNewFile *.nginx set ft=nginx
au BufRead,BufNewFile */etc/nginx/* set ft=nginx
au BufRead,BufNewFile */usr/local/nginx/conf/* set ft=nginx
au BufRead,BufNewFile nginx.conf set ft=nginx

" Highlight current line
set cursorline
" Highlight current column
" set cursorcolumn
" Command separated list of columns to highlight
set colorcolumn=100

" Open netrw if no files were given to vim.
augroup ProjectDrawer
    autocmd!
    autocmd VimEnter * if argc() == 0 | Explore! | endif
augroup END

" Close netrw if last window open.
augroup netrw_close
    autocmd!
    autocmd WinEnter * if winnr("$") == 1 && getbufvar(winbufnr(winnr()), "&filetype") == "netrw" | q | endif
augroup END

" Script to toggle netrw with <Ctrl-n>
function! ToggleNetrw()
    let i = bufnr("$")
    let wasOpen = 0
    while (i >= 1)
        if(getbufvar(i, "&filetype") == "netrw")
            silent exe "bwipeout " . i
            let wasOpen = 1
        endif
        let i-=1
    endwhile
    if !wasOpen
        silent Vexplore
    endif
endfunction
noremap <silent> <C-n> :call ToggleNetrw()<CR>

" Use a tree display
let g:netrw_liststyle = 3
" Don't display to banner at the top
let g:netrw_banner = 0
" Open selected files in the previous window
let g:netrw_browse_split = 4
let g:netrw_altv = 1
" Set netrw width percentage
let g:netrw_winsize = 18
" Enable the following hide patterns.
let g:netrw_hide = 1
" Hidden patterns. Repect gitignores and dotfiles.
let g:netrw_list_hide = netrw_gitignore#Hide()
let g:netrw_list_hide .= ',\(^\|\s\s\)\zs\.\S\+'
let g:netrw_list_hide .= ',.*\.swp$'

let g:airline_theme='gruvbox'
let g:airline#extensions#ale#enabled = 1
let g:airline#extensions#whitespace#enabled = 1
let g:airline#extensions#vimtex#enabled = 1
" Show opened buffers at the top of the screen.
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#fnamemod = ':t'
" Do not show the default mode indicator, because airline's is better.
" BUG: If the default indicator is left, I can leave INSERT mode with a single
" <C-c>, but I need to press it twice to make the indicator change.
set noshowmode

" Open a new empty buffer. Current buffer must be written.
nmap <C-t> :enew<CR>
" Move to the next buffer.
nmap <C-l> :bnext<CR>
" Move to the previous buffer.
nmap <C-h> :bprevious<CR>

" When creating a new window, make it equal sized to any currently open
" windows. E.g., when using vsplit multiple times.
set equalalways

" Linter settings
let g:ale_fixers = {
\   'python': ['pylint'],
\}

let g:ale_completion_enabled = 1
let g:ale_sign_column_always = 1
let g:ale_c_gcc_options = '-std=c11 -Wall -Wextra -Wpedantic'
let g:ale_cpp_gcc_options = '-std=c++17 -Wall -Wextra -Wpedantic'
